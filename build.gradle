plugins {
    id 'java'
    id "io.qameta.allure" version "2.8.1"
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven { url "http://palantir.bintray.com/releases" }
}

dependencies {
    // Use TestNG framework, also requires calling test.useTestNG() below
    implementation('org.testng:testng:6.14.3')

    //google
    implementation('com.google.guava:guava:28.0-jre')
    implementation('com.google.inject:guice:4.2.3')
    implementation("com.google.protobuf:protobuf-java:3.9.1")

    // retrofit
    implementation('com.squareup.retrofit2:retrofit:2.9.0')
    implementation('com.squareup.retrofit2:converter-gson:2.9.0')

    //okhttp3
    implementation('com.squareup.okhttp3:logging-interceptor:3.8.0')

    //grpc
    implementation('io.grpc:grpc-all:1.19.0')

    //lombok
    implementation('org.projectlombok:lombok:1.18.12')
    annotationProcessor('org.projectlombok:lombok:1.18.12')
    testImplementation('org.projectlombok:lombok:1.18.12')
    testAnnotationProcessor('org.projectlombok:lombok:1.18.12')

    //jboss
    implementation('org.jboss.aerogear:aerogear-otp-java:1.0.0')

    //allure
    implementation('io.qameta.allure:allure-gradle:2.8.1')
    implementation('io.qameta.allure:allure-java-commons:2.13.2')
    implementation('io.qameta.allure:allure-testng:2.13.2')
    implementation('io.qameta.allure:allure-okhttp3:2.10.0')
    implementation('io.qameta.allure:allure-rest-assured:2.10.0')

    //logger
    implementation('org.apache.logging.log4j:log4j-api:2.8')
    implementation('org.apache.logging.log4j:log4j-core:2.8')
    implementation('org.apache.logging.log4j:log4j-slf4j-impl:2.13.3')

    //palantir
    implementation('com.palantir.roboslack:roboslack-webhook-api:1.3.0')
    implementation('com.palantir.roboslack:roboslack-api:1.3.0')
    implementation('com.palantir.roboslack:roboslack-webhook:1.3.0')

    //github
    implementation('com.github.javafaker:javafaker:1.0.2')

    //appium
    implementation 'io.appium:java-client:7.5.1'

    //testvagrant
    implementation 'com.testvagrant.optimuscloud:remote-drivers:0.0.3'
    implementation('com.testvagrant.optimus:optimus-lite:0.0.4-beta')

    //selenium
    implementation('org.seleniumhq.selenium:selenium-java:3.141.59')
    implementation('io.github.bonigarcia:webdrivermanager:4.0.0')

    implementation('org.postgresql:postgresql:42.2.1')
    implementation('commons-dbutils:commons-dbutils:1.7')
    implementation('org.jeasy:easy-random:4.2.0')
    implementation('org.jdbi:jdbi:2.78')
    implementation("javax.annotation:javax.annotation-api:1.3.2")

    // generic wait
    implementation('org.awaitility:awaitility:4.0.3')
}

allure {
    version = '2.8.1'
    autoconfigure = true
    aspectjweaver = true
    allureJavaVersion = '2.10.0'
    reportDir = new File(project.projectDir as File, '/allure-report/ui')
    resultsDir = new File(project.projectDir as File, '/allure-report/results')
    clean = true
}

tasks.withType(Test) {
    systemProperties = [
            env                   : System.getProperty('env', 'staging'),
            tags                  : System.getProperty('tags', 'smoke'),
            browser               : System.getProperty('browser', 'chrome'),
            runMode               : System.getProperty('runMode', 'headless'),
            logs                  : System.getProperty('logs', 'false'),
            timeline              : System.getProperty('timeline', 'true'),
            //mobile
            testFeed              : System.getProperty('testFeed', 'calculator'),
            hub                   : System.getProperty('hub', 'browserstack'),
            target                : System.getProperty('target', 'local'),
            slackNotif            : System.getProperty('slackNotif', 'false'), //Global => On Failures
            slackNotifyMeEverytime: System.getProperty('slackNotifyMeEverytime', 'false')
    ]
}


task runWebTests(type: Test) {
    outputs.upToDateWhen { false }
    useTestNG {
        parallel = "classes"
        threadCount = 2
        includeGroups System.getProperty("tags", "web")
        testLogging.showStandardStreams = true
        useDefaultListeners true
        listeners << 'com.testvagrant.ekam.commons.listeners.LogListener'
        listeners << 'com.testvagrant.ekam.web.listeners.WebDriverListener'
        listeners << 'com.testvagrant.ekam.integrations.slack.SlackListener'
        listeners << 'com.testvagrant.ekam.commons.listeners.ReportListener'
        outputDirectory = file("$buildDir/" + System.getProperty('tags', 'NONE'))
    }
}

task runMobileTests(type: Test) {
    outputs.upToDateWhen { false }
    useTestNG {
        parallel = "methods"
        threadCount = 1
        includeGroups System.getProperty("tags", "mobile")
        testLogging.showStandardStreams = true
        useDefaultListeners true
        listeners << 'com.testvagrant.ekam.mobile.listeners.MobileDriverListener'
        listeners << 'com.testvagrant.ekam.commons.listeners.LogListener'
        listeners << 'com.testvagrant.ekam.commons.listeners.ReportListener'
        listeners << 'com.testvagrant.ekam.integrations.slack.SlackListener'
        outputDirectory = file("$buildDir/" + System.getProperty('tags', 'NONE'))
    }
}

task runApiTests(type: Test) {
    outputs.upToDateWhen { false }
    useTestNG {
        parallel = "classes"
        threadCount = 1
        includeGroups System.getProperty("tags", "api")
        testLogging.showStandardStreams = true
        useDefaultListeners true
        listeners << 'com.testvagrant.ekam.integrations.slack.SlackListener'
        outputDirectory = file("$buildDir/" + System.getProperty('tags', 'NONE'))
    }
}

task runDBTests(type: Test) {
    outputs.upToDateWhen { false }
    useTestNG {
        parallel = "classes"
        threadCount = 1
        includeGroups System.getProperty("tags", "db")
        testLogging.showStandardStreams = true
        useDefaultListeners true
        listeners << 'com.testvagrant.ekam.integrations.slack.SlackListener'
        outputDirectory = file("$buildDir/" + System.getProperty('tags', 'NONE'))
    }
}