plugins {
    id 'java'
    id "io.qameta.allure" version "2.8.1"
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven { url "http://palantir.bintray.com/releases" }
}

dependencies {
    // Use TestNG framework, also requires calling test.useTestNG() below
    implementation('org.testng:testng:6.14.3')

    // cucumber
    implementation('io.cucumber:cucumber-java:4.8.1')
    implementation('io.cucumber:cucumber-testng:4.8.1')

    //google
    implementation('com.google.guava:guava:28.0-jre')
    implementation('com.google.inject:guice:4.2.3')
    implementation("com.google.protobuf:protobuf-java:3.9.1")

    // retrofit
    implementation('com.squareup.retrofit2:retrofit:2.9.0')
    implementation('com.squareup.retrofit2:converter-gson:2.9.0')

    //okhttp3
    implementation('com.squareup.okhttp3:logging-interceptor:3.8.0')

    //grpc
    implementation('io.grpc:grpc-all:1.19.0')

    //lombok
    implementation('org.projectlombok:lombok:1.18.12')
    annotationProcessor('org.projectlombok:lombok:1.18.12')
    testImplementation('org.projectlombok:lombok:1.18.12')
    testAnnotationProcessor('org.projectlombok:lombok:1.18.12')

    //jboss
    implementation('org.jboss.aerogear:aerogear-otp-java:1.0.0')

    //allure
    implementation('io.qameta.allure:allure-gradle:2.8.1')
    implementation('io.qameta.allure:allure-java-commons:2.13.2')
    implementation('io.qameta.allure:allure-testng:2.13.2')
    implementation('io.qameta.allure:allure-okhttp3:2.10.0')
    implementation('io.qameta.allure:allure-rest-assured:2.10.0')

    //logger
    implementation('org.apache.logging.log4j:log4j-api:2.8')
    implementation('org.apache.logging.log4j:log4j-core:2.8')
    implementation('org.apache.logging.log4j:log4j-slf4j-impl:2.13.3')

    //palantir
    implementation('com.palantir.roboslack:roboslack-webhook-api:1.3.0')
    implementation('com.palantir.roboslack:roboslack-api:1.3.0')
    implementation('com.palantir.roboslack:roboslack-webhook:1.3.0')

    //github
    implementation('com.github.javafaker:javafaker:1.0.2')

    //appium
    implementation('io.appium:java-client:7.5.1')

    //testvagrant
    implementation('com.testvagrant.optimus:optimus-lite:0.1.6-beta')
    implementation('com.testvagrant.optimus:optimus-dashboard-lite:0.0.3-alpha')
    implementation('com.testvagrant.ekam:ekam-api:0.0.2-alpha')

    //selenium
    implementation('org.seleniumhq.selenium:selenium-java:3.141.59')
    implementation('io.github.bonigarcia:webdrivermanager:4.0.0')

    implementation('org.postgresql:postgresql:42.2.1')
    implementation('commons-dbutils:commons-dbutils:1.7')
    implementation('org.jeasy:easy-random:4.2.0')
    implementation('org.jdbi:jdbi:2.78')
    implementation("javax.annotation:javax.annotation-api:1.3.2")

    // generic wait
    implementation('org.awaitility:awaitility:4.0.3')
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'com.testvagrant.ekam'
            artifactId 'ekam'
            version '0.0.7-alpha'

            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Optimus Lite API to manage test devices and create appium driver based on platform'
            name 'Optimus Lite'
            url 'https://gitlab.com/testvagrant/automation-studio/ekam'
            organization {
                name 'com.testvagrant.optimus'
                url 'https://gitlab.com/testvagrant/automation-studio'
            }
            issueManagement {
                system 'GitLab'
                url 'https://gitlab.com/testvagrant/automation-studio/ekam/issues'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'https://gitlab.com/testvagrant/automation-studio/ekam/-/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://gitlab.com/testvagrant/automation-studio/ekam'
                connection 'https://gitlab.com/testvagrant/automation-studio/ekam.git'
                developerConnection 'git@gitlab.com:testvagrant/automation-studio/ekam.git'
            }
            developers {
                developer {
                    name 'Sudarshan GS'
                }
                developer {
                    name 'Krishnanand B'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
}


signing {
    sign publishing.publications
}